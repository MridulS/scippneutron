# ~~~
# SPDX-License-Identifier: BSD-3-Clause
# Copyright (c) 2022 Scipp contributors (https://github.com/scipp)
# ~~~
cmake_minimum_required(VERSION 3.16)
execute_process(
  COMMAND git describe --tags --abbrev=0
  WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
  OUTPUT_VARIABLE SCIPPNEUTRON_VERSION
  OUTPUT_STRIP_TRAILING_WHITESPACE
)
project(
  scippneutron
  VERSION ${SCIPPNEUTRON_VERSION}
  LANGUAGES CXX
)

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE
      "Release"
      CACHE
        STRING
        "Choose the type of build, options are: Debug Release RelWithDebInfo MinSizeRel."
        FORCE
  )
endif()

set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")

# Custom install target for docs to depend on.
add_custom_target(
  install-scippneutron COMMAND cmake --build ${CMAKE_CURRENT_BINARY_DIR}
                               --target install
)

include(docs)
add_docs_target(docs BUILDER html DEPENDS install-scippneutron)
add_docs_target(doctest BUILDER doctest DEPENDS docs)
add_docs_target(linkcheck BUILDER linkcheck DEPENDS docs)
add_custom_target(
  cleanup_docs_html
  COMMENT "Remove unnecessary .ipynb files from built HTML documentation."
  COMMAND find "${CMAKE_BINARY_DIR}/html" -type f -name "*.ipynb" -not -path
          "${CMAKE_BINARY_DIR}/html/_sources/*" -delete
  VERBATIM
)
add_dependencies(cleanup_docs_html docs)

include(GNUInstallDirs)
if(DEFINED ENV{SP_DIR}) # Defined by conda-build
  file(TO_CMAKE_PATH $ENV{SP_DIR}/scippneutron PYTHONDIR)
  file(TO_CMAKE_PATH $ENV{SP_DIR} ARCHIVEDIR)
else()
  set(PYTHONDIR scippneutron)
  set(ARCHIVEDIR .)
endif()
set(INCLUDEDIR ${CMAKE_INSTALL_INCLUDEDIR})

enable_testing()

add_subdirectory(lib)

install(DIRECTORY "src/scippneutron/" DESTINATION ${PYTHONDIR})
